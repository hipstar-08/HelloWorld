*** Settings ***
Force Tags        regression    jybot    pybot
Test Template     Run And Check Include And Exclude
Resource          atest_resource.txt

*** Variables ***
@{DATA SOURCES}   tags/include_and_exclude.txt
${ESCAPES}        --escape star:STAR --escape quest:QUEST --escape amp:AMP
@{INCL_ALL}       Incl-1    Incl-12    Incl-123
@{EXCL_ALL}       Excl-1    Excl-12    Excl-123
@{ALL}            @{INCL_ALL}    @{EXCL_ALL}

*** Test Cases ***
No Includes Or Excludes
    ${EMPTY}    @{ALL}

One Include
    --include incl1    @{INCL_ALL}

Matching And Non Matching Includes
    -i INCL3 -i nonexisting    Incl-123

More Includes
    -i incl2 --include incl3 -i _ --include incl2    Incl-12    Incl-123

Includes With AND
    [Documentation]    Testing including like "--include tag1&tag2" both with "&" and "AND"
    --include incl1ANDincl2    Incl-12    Incl-123
    -i incl1AMPincl2AMPincl3    Incl-123

Include With Patterns
    --include incl?    @{INCL_ALL}
    -i STARcl3 -i iSTAR2    Incl-12    Incl-123    Excl-123
    -i i?*3ANDFORCE --include iSTAR    @{INCL_ALL}

One Exclude
    --exclude excl1    @{INCL_ALL}

Matching And Non Matching Excludes
    -e EXCL3 -e nonexisting    @{INCL_ALL}    Excl-1    Excl-12

More Excludes
    --exclude excl3 -e excl2    @{INCL_ALL}    Excl-1

Exclude With AND
    --exclude excl1AMPexcl2    @{INCL_ALL}    Excl-1
    -e excl1AMPexcl2ANDexcl3    @{INCL_ALL}    Excl-1    Excl-12

Exclude With Patterns
    --exclude exc??    @{INCL_ALL}
    -e STAR3 -e eSTAR2 -e eSTAR1    Incl-1    Incl-12

Include And Exclude
    [Documentation]    Include and exclude together with and without patterns and ANDing
    -i force --exclude excl2    @{INCL_ALL}    Excl-1
    --include STARcl2 -i nonex -e e???2 -i forceANDi*1    @{INCL_ALL}

Include with NOT
    --include incl1NOTincl3    Incl-1    Incl-12

Exclude with NOT
    --exclude excl1NOTexcl2ANDexcl3    Excl-123    @{INCL_ALL}

Include and Exclude with NOT
    --include incl1NOTincl3 --exclude incl1NOTincl2    Incl-12

Select tests without any tags
    [Setup]    Set Test Variable    @{DATA SOURCES}    tags/no_force_no_default_tags.txt
    --exclude STAR    No Own Tags No Force Nor Default    Own Tags Empty No Force Nor Default

Select tests with any tag
    [Setup]    Set Test Variable    @{DATA SOURCES}    tags/no_force_no_default_tags.txt
    --include STAR    Own Tags No Force Nor Default

Non Matching Include
    [Template]    Run And Check Error
    --include nonex    tag 'nonex'
    --include nonex -i nonex2    tags 'nonex' or 'nonex2'

Non Matching Exclude
    --exclude nonexisting -e nonex2 -e nonex3    @{ALL}

Non Matching Include And Exclude
    [Template]    Run And Check Error
    -i nonex -e nonex2    tag 'nonex' and without tag 'nonex2'
    --include nonex -i incl? -e STAR1 -e STAR2 -e STAR3    tags 'nonex' or 'incl?' and without tags '*1', '*2' or '*3'

Non Matching When Running Multiple Suites
    [Setup]    Set Test Variable    @{DATA SOURCES}    misc/pass_and_fail.txt    misc/normal.txt
    [Template]    Run And Check Error
    --include nonex    tag 'nonex'    Pass And Fail & Normal
    --include nonex --name MyName   tag 'nonex'    MyName

*** Keywords ***
Run And Check Include And Exclude
    [Arguments]    ${params}    @{tests}
    Run Tests    ${params} ${ESCAPES}    @{DATA SOURCES}
    Stderr Should Be Empty
    Check Suite Contains Tests    ${SUITE}    @{tests}

Run And Check Error
    [Arguments]    ${params}    ${filter_msg}    ${suite name}=Include And Exclude
    Run Tests Without Processing Output    ${params} ${ESCAPES}    @{DATA SOURCES}
    Stderr Should Be Equal To    SEPARATOR=
    ...    [ ERROR ] Suite '${suite name}' contains no tests with ${filter_msg}.
    ...    ${USAGE TIP}\n
    File Should Not Exist    ${OUTFILE}
