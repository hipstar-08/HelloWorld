*** Settings ***
Force Tags        pybot    jybot   regression
Resource          tidy_resource.txt
Test Setup        Create Directory     ${TEMP}
Test Teardown     Remove Directory     ${TEMP}    recursive=True

*** Test cases ***
Tidying single test case file
    [Documentation]   Test tidying to different formats
    [Template]    Run tidy with golden file and check result
    ${EMPTY}            golden.txt
    --usepipes -f txt   golden_pipes.txt
    --format tsv        golden.tsv
    --format html       golden.html
    --for robot         golden.txt
    --spacecount 2      golden_two_spaces.txt

Tidying single resource file
    [Template]    Run tidy with golden resource file and check result
    ${EMPTY}    golden_resource.txt
    -p    golden_pipes_resource.txt
    -f tsv    golden_resource.tsv
    --FORMAT html    golden_resource.html
    --FOR ROBOT    golden_resource.txt

Tidying single init file
    Run tidy and check result    ${EMPTY}    __init__.txt
    File Should Exist    ${TEMP FILE}

Tidying single file without output file prints output to console
    [Documentation]    Depending on console encoding, non-ASCII characters may not be shown correctly
    ${stdout} =    Run tidy    ${EMPTY}    golden.txt    output=${NONE}
    Compare tidy results    ${stdout}    golden.txt    \\s+Log Many\\s+Non-ASCII:.*\\s+\\$\\{CURDIR\\}
    File Should Not Exist    ${TEMP FILE}

Default format is got from output file
    Run tidy    ${EMPTY}    ${DATA}/golden.txt    ${TEMP}/golden.html
    Compare tidy results    ${TEMP}/golden.html    ${DATA}/golden.html

Tidying directory
    [Setup]    Copy Directory    ${DATA}/tests    ${TEMP}/tests
    ${output_before}=    Run Robot Directly    ${DATA}/tests
    Run Tidy    --recursive --format TSV    ${TEMP}/tests
    Check file count    ${TEMP}/tests    *.tsv    2
    Check file count    ${TEMP}/tests/sub    *.tsv    1
    Check file count    ${TEMP}/tests    *.txt    0
    Check file count    ${TEMP}/tests/sub    *.txt    0
    Files Should Have $CURDIR    ${TEMP}/tests
    Files Should Have $CURDIR    ${TEMP}/tests/sub
    ${output_after}=    Run Robot Directly    ${TEMP}/tests
    Should Be Equal    ${output_before}    ${output_after}

Custom headers are preserved and tables aligned accordingly
    Run tidy and check result    ${EMPTY}     golden_with_headers.txt

Running Tidy as a script
    Run tidy as a script and check result    ${EMPTY}    golden.txt


*** Keywords ***
Files Should Have $CURDIR
    [Arguments]    ${directory}
    @{paths} =    List Files In Directory    ${directory}    absolute=True
    :FOR    ${path}    IN    @{paths}
    \    ${content} =    Get File    ${path}
    \    Should Contain    ${content}    $\{CURDIR}
