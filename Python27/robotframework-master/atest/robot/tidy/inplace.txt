*** Settings ***
Force Tags        pybot    jybot   regression
Resource          tidy_resource.txt
Test Setup        Create Directory     ${TEMP}
Test Teardown     Remove Directory     ${TEMP}    recursive=True

*** Test Cases ***

Tidying single file in place
    [Setup]    Copy File    ${DATA}/golden.txt    ${TEMP}/golden.txt
    Run tidy    --inplace --usepipes    ${TEMP}/golden.txt
    Compare tidy results    ${TEMP}/golden.txt    ${DATA}/golden_pipes.txt
    Check file count    ${TEMP}    *.txt    1

Tidying single file in place and change format
    [Setup]    Copy File    ${DATA}/golden.txt    ${TEMP}/golden.txt
    Run tidy    -i -f html    ${TEMP}/golden.txt
    Compare tidy results    ${TEMP}/golden.html    ${DATA}/golden.html
    Check file count    ${TEMP}    *.html    1
    Check file count    ${TEMP}    *.txt    0

Tidying many files in place
    Copy File    ${DATA}/golden_pipes.txt    ${TEMP}/
    Copy File    ${DATA}/golden.tsv    ${TEMP}/
    Run tidy    --InPlace --ForMat HtmL   ${TEMP}/golden*
    Check file count    ${TEMP}    *.html    2
    Check file count    ${TEMP}    *.txt    0
    Check file count    ${TEMP}    *.tsv    0
