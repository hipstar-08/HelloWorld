*** Settings ***
Suite Setup     Suite initialization
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt

*** Variables ***
${RUN FAILED FROM}    %{TEMPDIR}${/}run-failed-output.xml

*** Test Cases ***
Check that passing is not executed
    Run Keyword And Expect Error  No test 'Passing' found*  Check Test Case   Passing

Check that failing is executed
    Check Test Case   Failing

Check that explicitly selected is executed
    Check Test Case   Selected

Check that excluded failing is not executed
    Run Keyword And Expect Error  No test 'Failing with tag' found*  Check Test Case   Failing with tag

Check that non-existing failing from output file is not executed
    Run Keyword And Expect Error  No test 'Only in one suite' found*  Check Test Case   Only in one suite

Check that failing test case from subsuite is executed
    Check Test Case   Really Failing

*** Keywords ***
Suite initialization
    Copy File  ${DATADIR}/cli/runfailed/runfailed1.txt  ${DATADIR}/cli/runfailed/suite/runfailed.txt
    Run Tests  ${EMPTY}  cli/runfailed/suite
    Copy File  ${OUTFILE}  ${RUN FAILED FROM}
    Copy File  ${DATADIR}/cli/runfailed/runfailed2.txt  ${DATADIR}/cli/runfailed/suite/runfailed.txt
    Run Tests  --runfailed ${RUN FAILED FROM} --test Selected --exclude tag  cli/runfailed/suite
    [Teardown]  Remove Files  ${RUN FAILED FROM}  ${DATADIR}/cli/runfailed/suite/runfailed.txt
